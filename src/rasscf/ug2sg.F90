!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!                                                                      *
! Copyright (C) 1990, Jeppe Olsen                                      *
!               1990, Markus P. Fuelscher                              *
!***********************************************************************

subroutine UG2SG(NROOTS,NCONF,NORB,NEL,IREFSM,IPRINT,ICONF,ISPIN,IORD,ICI,JCJ,CCI,MXROOTS)
! AUTHOR:  J. OLSEN AND M.P. FUELSCHER
!          UNIV. OF LUND, SWEDEN 1990
!
! PURPOSE: CONSTRUCT THE REINDEXING ARRAY WHICH REORDERS
!          THE CSFS GENERATED BY THE DETERMINANT CODE INTO
!          THE SPLIT GRAPH GUGA ORDER. THEREFORE, CONSTRUCT
!          FOR EACH CSF THE CORRESPONDING
!          STEP VECTOR AND PASS IT TO THE FUNCTIONS IPHASE
!          AND ISGNUM WHICH COMPUTES THE THE PHASE FACTOR
!          INVOLVED WHEN GOING FROM THE SYMMETRIC TO THE
!          UNITARY GROUP AND THE SPLIT ORDERING NUMBER.

use gugx, only: SGS, CIS, EXS
use output_ras, only: LF
use spinfo, only: NTYP, MINOP, NCNFTP, NCSFTP
use Molcas, only: MxAct
use RASDim, only: MxRef

implicit none
integer NROOTS, NCONF, NORB, NEL, IREFSM, IPRINT, MXROOTS
integer ICONF(*), ISPIN(*)
integer IORD(*)
integer ICI(MXROOTS,*), JCJ(MXROOTS,*)
real*8 CCI(MXROOTS,*)
integer IWALK(mxAct)
integer KCNF(mxAct)
integer, external :: IPHASE
integer nVert, nLev, nMidV, MxUp, MxDwn
integer K, L, KREF, KROOT, ICSFJP, ICNBS0, IPBAS, ITYP, IOPEN, ICL, IC, ICNBS, IICSF, ICSBAS, IIBOP, IIBCL, JOCC, KOCC, ISG, IP, &
        LPRINT, I, ISGNUM, KORB
real*8 PHASE

nLev = SGS%nLev
nVert = SGS%nVert
nMidV = CIS%nMidV
MxUp = SGS%MxUp
MxDwn = SGS%MxDwn

! JCJ IS A TEMPORARY COPY OF ICI AND WILL OBTAIN THE SELECTED REFERENCE
! NUMBERS IN THE SYMMETRIC GROUP NUMBERING

if (IPRINT >= 5) then
  write(LF,*)
  write(LF,*) ' SPLIT GRAPH GUGA CONFIGURATION NUMBERS:'
  do K=1,NROOTS
    write(LF,'(A,I2,A,5I8)') ' ROOT',K,' CSFs:',(ICI(K,L),L=1,5)
  end do
end if

do kRef=1,mxRef
  do kRoot=1,mxRoots
    JCJ(kRoot,kRef) = 0
  end do
end do

! LOOP OVER CONFIGURATIONS TYPES

ICSFJP = 0
ICNBS0 = 0 ! dummy initialize
IPBAS = 0 ! dummy initialize
do ITYP=1,NTYP
  IOPEN = ITYP+MINOP-1
  ICL = (NEL-IOPEN)/2
  ! BASE ADDRESS FOR CONFIGURATION OF THIS TYPE
  if (ITYP == 1) then
    ICNBS0 = 1
  else
    ICNBS0 = ICNBS0+NCNFTP(ITYP-1,IREFSM)*(NEL+IOPEN-1)/2
  end if
  ! BASE ADDRESS FOR PROTOTYPE SPIN COUPLINGS
  if (ITYP == 1) then
    IPBAS = 1
  else
    IPBAS = IPBAS+NCSFTP(ITYP-1)*(IOPEN-1)
  end if

  ! LOOP OVER NUMBER OF CONFIGURATIONS OF TYPE ITYP AND PROTOTYPE
  ! SPIN COUPLINGS

  do IC=1,NCNFTP(ITYP,IREFSM)
    ICNBS = ICNBS0+(IC-1)*(IOPEN+ICL)
    do IICSF=1,NCSFTP(ITYP)
      ICSFJP = ICSFJP+1
      ICSBAS = IPBAS+(IICSF-1)*IOPEN
      !PAM04 The following lines have been replaced....
      ! COMPUTE STEP VECTOR
      !call STEPVEC(ICONF(ICNBS),ICONF(ICNBS+ICL),ICL,IOPEN,ISPIN(ICSBAS),NORB,IWALK)
      !PAM04 ... because of new convention for representing SG config, we
      ! need to arrange orbital indices in form used by RASSCF codes:
      !. Obtain configuration in standard RASSCF form
      IIBOP = 1
      IIBCL = 1
      JOCC = ICL+IOPEN
      do KOCC=0,JOCC-1
        KORB = ICONF(ICNBS+KOCC)
        if (KORB < 0) then
          !. Doubly occupied orbitals
          KCNF(IIBCL) = abs(KORB)
          IIBCL = IIBCL+1
        else
          !. Singly occupied orbital
          KCNF(ICL+IIBOP) = KORB
          IIBOP = IIBOP+1
        end if
      end do
      ! COMPUTE STEP VECTOR
      call STEPVEC(KCNF(1),KCNF(1+ICL),ICL,IOPEN,ISPIN(ICSBAS),NORB,IWALK)
      !PAM04 End of replacement code.
      ! GET SPLIT GRAPH ORDERING NUMBER
      ISG = ISGNUM(NLEV,NVERT,SGS%MIDLEV,SGS%MVSta,NMIDV,MXUP,MXDWN,SGS%DOWN,SGS%UP,SGS%DAW,SGS%RAW,EXS%USGN,EXS%LSGN,IWALK)

      ! GET PHASE PHASE FACTOR
      IP = IPHASE(NLEV,NVERT,SGS%DRT,SGS%UP,IWALK)
      ! UPDATE REINDEXING TABLE
      IORD(ICSFJP) = ISG*IP
    end do
  end do
end do

if (IPRINT >= 5) then
  LPRINT = min(200,NCONF)
  write(LF,*)
  write(LF,*) ' INDEX TABLE IN SUBROUTINE REORD'
  write(LF,'(10I8)') (IORD(I),I=1,LPRINT)
  write(LF,*)
end if

! REPLACE CONFIGURATION NUMBERS

do IC=1,NCONF
  ISG = IORD(IC)
  PHASE = 1.0d0
  if (ISG < 0) PHASE = -1.0d0
  ISG = abs(ISG)
  do K=1,NROOTS
    do L=1,MXREF
      if (ICI(K,L) == ISG) then
        JCJ(K,L) = IC
        CCI(K,L) = CCI(K,L)*PHASE
      end if
    end do
  end do
end do

if (IPRINT >= 5) then
  write(LF,*) ' SYMMETRIC GROUP CONFIGURATION NUMBERS:'
  do K=1,NROOTS
    write(LF,'(A,I2,A,5I6)') ' ROOT',K,' CSFs:',(JCJ(K,L),L=1,5)
  end do
  write(LF,*)
end if

return

end subroutine UG2SG
