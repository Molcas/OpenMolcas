!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!                                                                      *
! Copyright (C) 1990,2023, Roland Lindh                                *
!               1990, IBM                                              *
!***********************************************************************

subroutine TNAI(Zeta,Eta,P,Q,rKapab,rKapcd,T,Fact,ZEInv,nT,IsChi,ChiI2,nOrdOp)
!***********************************************************************
!                                                                      *
! Object: to compute entities for the nuclear attraction integrals     *
!         which are used in the Rys quadrature to evaluate these       *
!         integrals.                                                   *
!                                                                      *
!     Author: Roland Lindh, IBM Almaden Research Center, San Jose, CA  *
!             March '90                                                *
!***********************************************************************

use Constants, only: One, Two, Three, Four, Pi
use Definitions, only: wp, iwp

implicit none
integer(kind=iwp), intent(in) :: nT, IsChi, nOrdOp
real(kind=wp), intent(in) :: Zeta(nT), Eta(nT), P(nT,3), Q(nT,3), rKapab(nT), rKapcd(nT), ChiI2
real(kind=wp), intent(out) :: T(nT), Fact(nT), ZEInv(nT)
integer(kind=iwp) :: iT
real(kind=wp) :: PQ2, Rho, tmp

#include "macros.fh"
unused_var(Eta)
unused_var(rKapcd)
unused_var(IsChi)
unused_var(ChiI2)

#ifdef _DEBUGPRINT_
call RecPrt(' Zeta in TNAI',' ',Zeta,nT,1)
call RecPrt(' Eta in TNAI',' ',Eta,nT,1)
call RecPrt(' P in TNAI',' ',P,nT,3)
call RecPrt(' Q in TNAI',' ',Q,nT,3)
call RecPrt(' Kab in TNAI',' ',rKapab,nT,1)
call RecPrt(' Kcd in TNAI',' ',rKapcd,nT,1)
#endif

select case (nOrdOp)

  case (0)

    do iT=1,nT
      tmp = One/Zeta(iT)
      ZEInv(iT) = tmp
      PQ2 = (P(iT,1)-Q(iT,1))**2+(P(iT,2)-Q(iT,2))**2+(P(iT,3)-Q(iT,3))**2
      Rho = Zeta(iT)
      T(iT) = Rho*PQ2
      Fact(iT) = Two*rKapab(iT)*Pi*tmp
    end do

  case (1)

    do iT=1,nT
      tmp = One/Zeta(iT)
      ZEInv(iT) = tmp
      PQ2 = (P(iT,1)-Q(iT,1))**2+(P(iT,2)-Q(iT,2))**2+(P(iT,3)-Q(iT,3))**2
      Rho = Zeta(iT)
      T(iT) = Rho*PQ2
      Fact(iT) = Two*rKapab(iT)*Pi*tmp*(Two*Rho)
    end do

  case (2)

    do iT=1,nT
      tmp = One/Zeta(iT)
      ZEInv(iT) = tmp
      PQ2 = (P(iT,1)-Q(iT,1))**2+(P(iT,2)-Q(iT,2))**2+(P(iT,3)-Q(iT,3))**2
      Rho = Zeta(iT)
      T(iT) = Rho*PQ2
      Fact(iT) = Two*rKapab(iT)*Pi*tmp*(Four*Rho**2/Three)
    end do

end select

#ifdef _DEBUGPRINT_
call RecPrt('Tvalue',' ',T,nT,1)
call RecPrt('Fact  ',' ',Fact,nT,1)
#endif

return

end subroutine TNAI
