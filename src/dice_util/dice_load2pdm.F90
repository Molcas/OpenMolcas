!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!                                                                      *
! Copyright (C) 2017, Quan Phung                                       *
!***********************************************************************
! Load text file 2RDM generated by DICE

subroutine dice_load2pdm( NAC, PT, CHEMROOT )

  IMPLICIT NONE
  INTEGER, INTENT(IN) :: NAC, CHEMROOT
  REAL*8, INTENT(OUT) :: PT( NAC, NAC, NAC, NAC )
  REAL*8 :: PTtemp

  CHARACTER(LEN=30) :: file_2rdm

  INTEGER :: i, idx1, idx2, idx3, idx4, irdm, lu, ierr
  INTEGER :: nact, isFreeUnit
  character(len=10) :: rootindex

  INTEGER :: nac4

  external isFreeUnit

  nac4 = nac**4
  call dcopy_(nac4,0.0d0,0,PT,1)
  PT = 0.0d0

  write(rootindex,"(I2)") chemroot-1
  file_2rdm="spatialRDM."//trim(adjustl(rootindex))//"."//trim(adjustl(rootindex))//".txt"
  file_2rdm=trim(adjustl(file_2rdm))

  call f_inquire(file_2rdm, irdm)
  if (irdm .EQ. 0) then
     write(6,'(1X,A15,I3,A16)') 'DICE> Root: ',CHEMROOT,' :: No 2RDM file'
     call abend()
  endif

  LU=isFreeUnit(40)
  call molcas_open(LU,file_2rdm)

  read(LU,*) nact

  if (nact .NE. NAC) then
    write(6,*) 'DICE: DB> Wrong number of active orbitals'
    call abend()
  endif

! Quan: Dice ignore all elements smaller than 1.0D-6
  do
    read(LU,*,IOSTAT=ierr) idx1, idx2, idx3, idx4, PTtemp
    if (ierr == 0) then
      PT( idx1+1, idx3+1, idx4+1, idx2+1 ) = PTtemp
    else
      exit
    endif
  enddo

  close(LU)

end subroutine
